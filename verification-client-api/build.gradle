apply from: "${rootDir}/gradle/publish-json-artifacts.gradle"
apply plugin: 'com.palantir.conjure'

sourceSets {
    generator
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    compile 'com.palantir.conjure:conjure-core'
    compile 'com.palantir.conjure.java.runtime:conjure-java-jackson-serialization'
    compile project("${project.name}-objects")
    compile project(':test-cases-api')
    compile sourceSets.generator.output

    generatorImplementation project(':test-cases-api')
    generatorImplementation project(':test-cases-api:test-cases-api-objects')
    generatorImplementation 'com.fasterxml.jackson.core:jackson-databind'
    generatorImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    generatorImplementation 'com.palantir.conjure.java.runtime:conjure-java-jackson-serialization'
    generatorImplementation 'com.google.guava:guava'
}

task generateConjureDefinitions(type: JavaExec) {
    description = "Generate the conjure service definition files"
    main = "com.palantir.conjure.verification.client.GenerateVerificationClientConjureDefinitions"
    classpath = sourceSets.generator.runtimeClasspath
    args rootProject.file('test-cases.yml'), 'src/main/conjure'

    inputs.file rootProject.file('test-cases.yml')
    outputs.dir 'src/main/conjure/generated'
}

// Ensure we generate definitions first before conjure tries to compile them
tasks.copyConjureSourcesIntoBuild {
    dependsOn tasks.generateConjureDefinitions
}

task compileTestCasesJson(type: JavaExec) {
    description = "Generate client-specific test cases"
    main = "com.palantir.conjure.verification.client.CompileVerificationClientTestCasesJson"
    classpath = sourceSets.main.runtimeClasspath
    args rootProject.file('test-cases.yml'), "$buildDir/test-cases.json"

    inputs.file rootProject.file('test-cases.yml')
    outputs.file "$buildDir/test-cases.json"
}

build.dependsOn compileTestCasesJson

publishing {
    publications {
        conjureIr(MavenPublication) {
            artifactId project.name
            artifact (compileIr.outputs.files.singleFile) {
                extension 'conjure.json'
                builtBy compileIr
            }
        }

        testCases(MavenPublication) {
            artifactId "verification-client-test-cases"
            artifact (compileTestCasesJson.outputs.files.singleFile) {
                extension 'json'
                builtBy compileTestCasesJson
            }
        }
    }
}


