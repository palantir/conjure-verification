apply plugin: 'com.palantir.conjure'
apply plugin: 'com.palantir.external-publish-conjure'
apply plugin: 'com.palantir.external-publish-custom'

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'com.palantir.conjure:conjure-core'
    implementation 'com.palantir.conjure.java.runtime:conjure-java-jackson-serialization'
    implementation project("${project.name}-objects")
    implementation project(':test-cases-internal')
    implementation project(':test-cases-internal:test-cases-internal-objects')
    
    runtimeOnly 'org.slf4j:slf4j-simple'

    modules {
        module 'javax.annotation:javax.annotation-api', {
            replacedBy('jakarta.annotation:jakarta.annotation-api', 'prefer the jakarta version')
        }
        module 'javax.ws.rs:javax.ws.rs-api', {
            replacedBy('jakarta.ws.rs:jakarta.ws.rs-api', 'prefer the jakarta version')
        }
        module 'org.glassfish.hk2.external:javax.inject', {
            replacedBy('javax.inject:javax.inject', 'prefer the javax version')
        }
    }
}

// Ensure we generate definitions first before conjure tries to compile them
tasks.copyConjureSourcesIntoBuild {
    dependsOn ':test-cases-internal:generateClientServices'
}

task compileTestCasesJson(type: JavaExec) {
    description = "Generate client-specific test cases"
    main = "com.palantir.conjure.verification.client.CompileVerificationClientTestCasesJson"
    classpath = sourceSets.main.runtimeClasspath
    args rootProject.file('master-test-cases.yml'), "$buildDir/test-cases.json"

    inputs.file rootProject.file('master-test-cases.yml')
    outputs.file "$buildDir/test-cases.json"
}

build.dependsOn compileTestCasesJson
task generate(dependsOn: compileTestCasesJson)

externalPublishing {
    publication('testCases') {
        artifactId 'verification-client-test-cases'
        artifact (compileTestCasesJson.outputs.files.singleFile) {
            extension 'json'
            builtBy compileTestCasesJson
        }
    }
}

sourceCompatibility = 11

