apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def variants = ['server', 'client']

bintray {
    user = System.env.BINTRAY_USERNAME
    key = System.env.BINTRAY_PASSWORD
    publish = true
    pkg {
        repo = 'releases'
        name = rootProject.name
        userOrg = 'palantir'
        licenses = ['Apache-2.0']
        publications = variants.collectMany { ["${it}DistTarLinux", "${it}DistTarOsx"] }
    }
}

publish.dependsOn bintrayUpload
bintrayUpload.onlyIf {
    versionDetails().isCleanTag
}

variants.each { String variant ->
    task "${variant}DistTarLinux"(type: Tar) {
        from "target/release/conjure-verification-${variant}"
        baseName = "verification-${variant}-linux"
        compression = Compression.GZIP
    }

    task "${variant}DistTarOsx"(type: Tar) {
        from "target/x86_64-apple-darwin/release/conjure-verification-${variant}"
        baseName = "verification-${variant}-osx"
        compression = Compression.GZIP
    }

    publishing {
        publications {
            "${variant}DistTarLinux"(MavenPublication) {
                artifactId "verification-$variant"
                artifact (tasks["${variant}DistTarLinux"]) {
                    classifier "linux"
                }
            }
            "${variant}DistTarOsx"(MavenPublication) {
                artifactId "verification-$variant"
                artifact (tasks["${variant}DistTarOsx"]) {
                    classifier "osx"
                }
            }
        }
    }
}

