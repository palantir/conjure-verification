apply from: "${rootDir}/gradle/publish-json-artifacts.gradle"
apply plugin: 'com.palantir.conjure'

sourceSets {
    generator
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    compile 'com.palantir.conjure:conjure-core'
    compile 'com.palantir.conjure.java.runtime:conjure-java-jackson-serialization'
    compile project("${project.name}-objects")
    compile project(':test-cases-internal')
    compile sourceSets.generator.output

    generatorImplementation project(':test-cases-internal')
    generatorImplementation project(':test-cases-internal:test-cases-internal-objects')
    generatorImplementation 'com.fasterxml.jackson.core:jackson-databind'
    generatorImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    generatorImplementation 'com.palantir.conjure.java.runtime:conjure-java-jackson-serialization'
    generatorImplementation 'com.google.guava:guava'
}

// Ensure we generate definitions first before conjure tries to compile them
tasks.copyConjureSourcesIntoBuild {
    dependsOn tasks.generateConjureDefinitions
}

task compileTestCasesJson(type: JavaExec) {
    description = "Generate server-specific test cases"
    main = "com.palantir.conjure.verification.server.CompileVerificationServerTestCasesJson"
    classpath = sourceSets.main.runtimeClasspath
    args rootProject.file('master-test-cases.yml'), "$buildDir/test-cases.json"

    inputs.file rootProject.file('master-test-cases.yml')
    outputs.file "$buildDir/test-cases.json"
}

build.dependsOn compileTestCasesJson
task generate(dependsOn: compileTestCasesJson)

publishing {
    publications {
        conjureIr(MavenPublication) {
            artifactId project.name
            artifact (compileIr.outputs.files.singleFile) {
                extension 'conjure.json'
                builtBy compileIr
            }
        }

        testCases(MavenPublication) {
            artifactId "verification-server-test-cases"
            artifact (compileTestCasesJson.outputs.files.singleFile) {
                extension 'json'
                builtBy compileTestCasesJson
            }
        }
    }
}
